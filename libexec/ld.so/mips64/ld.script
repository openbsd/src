PHDRS
{
	rodata	PT_LOAD FILEHDR PHDRS FLAGS (4);
	btext	PT_LOAD FLAGS (0x08000005);
	text	PT_LOAD FLAGS (1);
	data	PT_LOAD;
	random	PT_OPENBSD_RANDOMIZE;
	syscalls PT_OPENBSD_SYSCALLS;
	relro	PT_GNU_RELRO;
	dynamic	PT_DYNAMIC;
	note	PT_NOTE;
}

SECTIONS
{
    . = 0 + SIZEOF_HEADERS;
    /* RODATA */
    .gnu.hash	: { *(.gnu.hash) } :rodata
    .dynsym	: { *(.dynsym) } :rodata
    .dynstr	: { *(.dynstr) } :rodata
    .rodata	: { *(.rodata .rodata.*) } :rodata
    .eh_frame	: { *(.eh_frame) } :rodata

    /* TEXT */
    . = ALIGN(0x4000);
    .boot.text	:
    {
	. = ALIGN(0x4000);
	boot_text_start = .;
	*(.boot.text)
	. = ALIGN(0x4000);
	boot_text_end = .;
    } :btext =0xefefefef
    . = ALIGN(0x4000);
    .text	: { *(.text .text.*) } :text =0xefefefef

    /* RELRO DATA */
    . = DATA_SEGMENT_ALIGN (0x10000, 0x4000);
    .openbsd.randomdata :
    {
	*(.openbsd.randomdata .openbsd.randomdata.*)
    } :data :relro :random
    .openbsd.syscalls :
    {
	*(.openbsd.syscalls .openbsd.syscalls.*)
    } :syscalls

    .data.rel.ro : { *(.data.rel.ro.local*) *(.data.rel.ro*) } :data :relro
    .dynamic	: { *(.dynamic) } :data :relro :dynamic
    _gp = ALIGN(16) + 0x7ff0;
    .got	: { *(.got.plt) *(.got) } :data :relro
    . = DATA_SEGMENT_RELRO_END (0, .);

    /* BOOTDATA */
    . = ALIGN(0x4000);
    boot_data_start = .;
    .rela.dyn       :
    {
	*(.rela.text .rela.text.*)
	*(.rela.rodata .rela.rodata.*)
	*(.rela.data .rela.data.*)
	*(.rela.got)
	*(.rela.bss .rela.bss.*)
    } :data
/* XXX .rela.plt is unused but cannot delete: ld.bfd zeros DT_RELASZ then! */
    .rela.plt	: { *(.rela.plt) } :data
    .note	: { *(.note.openbsd.*) } :data :note
    .hash	: { *(.hash) } :data
    .boot.data	: { *(.boot.data .boot.data.*) } :data
    boot_data_end = .;

    /* DATA */
    . = ALIGN(0x4000);
    .sdata	: { *(.sdata .sdata.*) } :data
    .data	: { *(.data .data.*) } :data
    .sbss	: { *(.sbss .sbss.*) } :data
    .bss	: { *(.dynbss) *(.bss .bss.*) *(COMMON) } :data
    . = DATA_SEGMENT_END (.);

    /DISCARD/	: { *(.note.GNU-stack) }
}
