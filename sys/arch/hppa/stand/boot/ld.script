/*	$OpenBSD: ld.script,v 1.2 1999/04/20 20:02:37 mickey Exp $	*/

OUTPUT_FORMAT("elf32-hppa")
OUTPUT_ARCH(hppa)
ENTRY(begin)

SECTIONS {

	/* Read-only sections, merged into text segment: */
	. = + SIZEOF_HEADERS;
	.text : {
		*(.text)
		*(.rodata)
		*(.rodata1)
		*($CODE$)
		etext = ABSOLUTE(.);
	} = 0x08000240 /* nop */

	/* Read-write sections, merged into data segment: */
	.data : {
		$global$ = .;
		*(.data)
		*(.data1)
		*(.sdata)
		*(.sdata2)
		*(.dynamic)
		CONSTRUCTORS
		edata = ABSOLUTE(.);
	} = 0

	.bss : {
		__bss_start = .;
		*(.dynbss)
		*(.bss)
		*(.sbss)
		*(.scommon)
		*(COMMON)
		end = ABSOLUTE(.);
		__bss_end = .;
	}

	/* don't need an unwind in the boot, unless we'll have a boot-ddb (; */
	/DISCARD/ :
	{
		*(.PARISC.unwind)
	}
}

