/*	$OpenBSD: ld.script,v 1.8 2016/09/03 15:04:10 deraadt Exp $	*/

/*
 * Copyright (c) 2013 Mark Kettenis <kettenis@openbsd.org>
 *
 * Permission to use, copy, modify, and distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

OUTPUT_FORMAT("elf64-sparc")
OUTPUT_ARCH(sparc:v9)
ENTRY(start)

PHDRS
{
	text PT_LOAD;
	data PT_LOAD;
	openbsd_randomize 0x65a3dbe6; /* PT_OPENBSD_RANDOMIZE */
	openbsd_bootdata  0x65a41be6; /* PT_OPENBSD_BOOTDATA */
}

SECTIONS
{
	. = 0x01000000;
	.text :
	{
		*(.text)
		PROVIDE(etext = .);
	} :text
	.rodata :
	{
		*(.rodata*)
	}
	.openbsd.randomdata :
	{
		*(.openbsd.randomdata)
	} :text :openbsd_randomize
	. = 0x01800000;
	.data :
	{
		*(.data)
	} :data
	.openbsd.bootdata :
	{
		*(.openbsd.bootdata)
	} :data :openbsd_bootdata
	.bss :
	{
		*(.bss)
		. = ALIGN(64 / 8);
	} :data
	. = ALIGN(64 / 8);
	PROVIDE (end = .);
}
